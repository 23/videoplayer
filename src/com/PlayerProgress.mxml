<?xml version="1.0" encoding="utf-8"?>                                                                                                                                    
<mx:Canvas 
	xmlns:mx="http://www.adobe.com/2006/mxml"
	horizontalScrollPolicy="off" 
	verticalScrollPolicy="off"
	useHandCursor="true" 
	buttonMode="true" 
	height="11"
	click="seekVideoTo(timeFromPoint(this.contentMouseX+30));" 
	clipContent="true"
	initialize="init()"
	>
	<!-- VideoProgress -->
	<mx:Canvas id="videoProgress" 
		width="100%"
		height="6" 
		top="5"
		left="0"
		right="{(videoTimeLabel.visible&amp;&amp;videoTimeLabel.text.length>0 ? Math.round((videoTimeLabel.width+5)/10)*10 : 0)}"
		resize="drawSections();"
		backgroundColor="white" 
		backgroundAlpha="0.3">
		<!-- mx:Image
			source="@Embed(source='assets/buttons/progress-background.swf')"
			maintainAspectRatio="false" 
			width="100%"
			height="4"
			/ -->
		<mx:Canvas id="videoBufferProgress" 
			x="0" 
			height="6" 
			backgroundColor="black" 
			backgroundAlpha="0.15" 
			/>
		<mx:Canvas id="videoPlayProgress" 
			x="0"
			height="6" 
			backgroundColor="white" 
			backgroundAlpha="1" 
			/>
	</mx:Canvas>					

	<mx:Label id="videoTimeLabel" 
		selectable="false"
		fontFamily="Helvetica"
		fontSize="10"
		height="17" 
		color="white"
		right="0"
		top="0"
		buttonMode="true"
		/>

	<!-- videoTime -->
	<mx:HBox id="videoDragHandle"
			 y="{3+ ((this.height-videoTimeLabel.height)/2)}" 
			 width="8"
			 height="6"
			 x="-8"
			 backgroundColor="white" 
			 useHandCursor="true"
			 buttonMode="true"
			 >
	</mx:HBox>

	<!-- Sections -->
	<mx:Canvas id="sectionContainer" top="0" left="0" width="100%" height="100%" horizontalScrollPolicy="off"></mx:Canvas>
	
	
	<mx:Script>
		<![CDATA[
			import com.philemonworks.flex.util.HashCollection;
			
			import mx.controls.VideoDisplay;
			import mx.core.Application;
			import mx.events.VideoEvent;
			[bindable] private var props:HashCollection = mx.core.FlexGlobals.topLevelApplication.props;
			[bindable] private var activeElement:HashCollection = mx.core.FlexGlobals.topLevelApplication.activeElement;

			var scrubberStartX:int = -1;
			var scrubberStartMouseX:int = -1;
			private function init():void {
				this.addEventListener(MouseEvent.MOUSE_MOVE, function(e:MouseEvent){
					if(scrubberStartMouseX<0) return;
					videoDragHandle.x = scrubberStartX + (e.stageX-scrubberStartMouseX);
					var x = scrubberStartX + (e.stageX-scrubberStartMouseX);
					videoTimeLabel.text = formatTime(timeFromPoint(x), _targetVideo.totalTime + activeElement.getNumber('start'));
				});
				this.addEventListener(MouseEvent.MOUSE_UP, function(e:MouseEvent){
					if(scrubberStartMouseX<0) return;
					scrubberStartMouseX = scrubberStartX = -1;
					var x = scrubberStartX + ((e.stageX-4)-scrubberStartMouseX);
					seekVideoTo(timeFromPoint(x));
				});
				videoDragHandle.addEventListener(MouseEvent.MOUSE_DOWN, function(e:MouseEvent){
					scrubberStartX = videoDragHandle.x;
					scrubberStartMouseX = e.stageX;
				});
			}

			// Component-specific functions and event-handlers
			private function formatTime(time:int, totalTime:int = -1, join:String = ' / '):String {
				if (time<0) return("");
				if (totalTime>=0) {
					return(Math.floor(time/60).toString() +':'+ (time%60<10?'0':'') + Math.round(time%60).toString() + join + Math.floor(totalTime/60).toString() +':'+ (totalTime%60<10?'0':'') + Math.round(totalTime%60).toString());
				} else {
					return(Math.floor(time/60).toString() +':'+ (time%60<10?'0':'') + Math.round(time%60).toString());
				}
			}
			private function timeFromPoint(x:int):int {
				return Math.round((x-20)/videoProgress.width*activeElement.getNumber('length'));
			}
            public function seekVideoTo(time:int):void {
            	if(!_targetVideo) return;
            	if(time>activeElement.getNumber('length')) return;
            	var approxBufferTime:Number = (_targetVideo.totalTime * (_targetVideo.bytesLoaded/_targetVideo.bytesTotal)) + activeElement.getNumber('start');
        		_targetVideo.bufferTime = 2; // default buffer time is two secs
            	if(time>new Number(activeElement.get('start')) && time<approxBufferTime) {
            		_targetVideo.playheadTime = time-activeElement.getNumber('start');
            		if(!_targetVideo.playing) this.parentApplication.playVideoElement();
            	} else {
            		if ((props.get('streaming') as Boolean)) {
            			activeElement.put('start', Math.floor(time));
            			activeElement.put('skip', '1');
            			_targetVideo.bufferTime = 0; // when skipping we'll skip the buffer
						if(_targetVideo.playing) _targetVideo.stop();
            			_targetVideo.source = getFullVideoSource();
						this.parentApplication.playVideoElement();
            		}
            	}
            }
            private function updateBuffer(e:Event):void {
            	if(!_targetVideo) return;
            	videoBufferProgress.x = activeElement.getNumber('start') / activeElement.getNumber('length') * videoProgress.width;
            	videoBufferProgress.width = _targetVideo.bytesLoaded/_targetVideo.bytesTotal*(videoProgress.width-videoBufferProgress.x);
            }
            private function updatePlay(e:Event):void {
            	if(!_targetVideo) return;
				videoPlayProgress.x = activeElement.getNumber('start') / activeElement.getNumber('length') * videoProgress.width;
				videoPlayProgress.width  = _targetVideo.playheadTime/_targetVideo.totalTime*(videoProgress.width-videoPlayProgress.x);
				if(scrubberStartMouseX<0) {
					videoDragHandle.x = videoPlayProgress.x + videoPlayProgress.width - 8;
					videoTimeLabel.text = formatTime(_targetVideo.playheadTime + activeElement.getNumber('start'), _targetVideo.totalTime + activeElement.getNumber('start'));
				}
            }

			private function getFullVideoSource():String {
				return(activeElement.getString('videoSource') + '?start=' + encodeURIComponent(activeElement.getString('start')) + '&skip=' + encodeURIComponent(activeElement.getString('skip')));
			}            
            		

			// Which video display are we binding to? (and attach to events)
			private var _targetVideo:VideoDisplay;
			public function set targetVideo(v:VideoDisplay):void {
				_targetVideo = v;
				v.addEventListener(VideoEvent.PLAYHEAD_UPDATE, updatePlay);
				v.addEventListener(ProgressEvent.PROGRESS, updateBuffer);
			}
			public function get targetVideo():VideoDisplay {return(_targetVideo);}
			
			private var _sections:Array = [];
			public function setSections(s:Array):void{
				_sections = s;
				drawSections();
			}			
			private function drawSections():void{
				// Clear current objects
				sectionContainer.removeAllChildren();
				// Add new ones
				for (var i:int=0; i<_sections.length; i++) {
					var s:Object = _sections[i];
					var section:PlayerProgressSection = new com.PlayerProgressSection;
					
					// Position section indicator
					section.width = 10;
					section.height = 10;
					section.y = 3;
					section.x = ((s.start_time/activeElement.getNumber('length')) * (videoProgress.width-30));
					section.title = s.title;
					section.startTime = s.start_time;
					
					// Listen to clicks
					section.addEventListener(MouseEvent.CLICK, function(e:Event):void{
						e.stopPropagation();
						seekVideoTo(e.currentTarget.startTime);
					});
					
					// Add to progress bar
					sectionContainer.addChild(section);
				}
			}
		]]>
	</mx:Script>	
</mx:Canvas>
